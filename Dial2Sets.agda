-----------------------------------------------------------------------
-- This file defines Dialâ‚‚(Sets) and its SMC structure.              --
-----------------------------------------------------------------------
module Dial2Sets where

open import prelude

-- The objects:
Obj : Setâ‚
Obj = Î£[ U âˆˆ Set ] (Î£[ X âˆˆ Set ] (U â†’ X â†’ Set))

-- The morphisms:
Hom : Obj â†’ Obj â†’ Set
Hom (U , X , Î±) (V , Y , Î²) =
  Î£[ f âˆˆ (U â†’ V) ]
    (Î£[ F âˆˆ (Y â†’ X) ] (âˆ€{u : U}{y : Y} â†’ Î± u (F y) â†’ Î² (f u) y))

-- Composition:
comp : {A B C : Obj} â†’ Hom A B â†’ Hom B C â†’ Hom A C
comp {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)} (f , F , pâ‚) (g , G , pâ‚‚) =
  (g âˆ˜ f , F âˆ˜ G , (Î» {u z} p-Î± â†’ pâ‚‚ (pâ‚ p-Î±)))

infixl 5 _â—‹_

_â—‹_ = comp

-- The contravariant hom-functor:
Homâ‚ :  {A' A B B' : Obj} â†’ Hom A' A â†’ Hom B B' â†’ Hom A B â†’ Hom A' B'
Homâ‚ f h g = comp f (comp g h)

-- The identity function:
id : {A : Obj} â†’ Hom A A 
id {(U , V , Î±)} = (id-set , id-set , id-set)

-- In this formalization we will only worry about proving that the
-- data of morphisms are equivalent, and not worry about the morphism
-- conditions.  This will make proofs shorter and faster.
--
-- If we have parallel morphisms (f,F) and (g,G) in which we know that
-- f = g and F = G, then the condition for (f,F) will imply the
-- condition of (g,G) and vice versa.  Thus, we can safly ignore it.
infix 4 _â‰¡h_

_â‰¡h_ : {A B : Obj} â†’ (f g : Hom A B) â†’ Set
_â‰¡h_ {(U , X , Î±)}{(V , Y , Î²)} (f , F , pâ‚) (g , G , pâ‚‚) = f â‰¡ g Ã— F â‰¡ G

-----------------------------------------------------------------------
-- Dialâ‚‚(Sets) is a SMC                                              --
-----------------------------------------------------------------------

-- The tensor functor: âŠ—
_âŠ—áµ£_ : âˆ€{U X V Y : Set} â†’ (U â†’ X â†’ Set) â†’ (V â†’ Y â†’ Set) â†’ ((U Ã— V) â†’ ((V â†’ X) Ã— (U â†’ Y)) â†’ Set)
_âŠ—áµ£_ Î± Î² (u , v) (f , g) = (Î± u (f v)) Ã— (Î² v (g u))

_âŠ—â‚’_ : (A B : Obj) â†’ Obj
(U , X , Î±) âŠ—â‚’ (V , Y , Î²) = ((U Ã— V) , ((V â†’ X) Ã— (U â†’ Y)) , Î± âŠ—áµ£ Î²)

FâŠ— : âˆ€{S Z W T V X U Y : Set}{f : U â†’ W}{F : Z â†’ X}{g : V â†’ S}{G : T â†’ Y} â†’ (S â†’ Z) Ã— (W â†’ T) â†’ (V â†’ X) Ã— (U â†’ Y)
FâŠ— {f = f}{F}{g}{G} (hâ‚ , hâ‚‚) = (Î» v â†’ F(hâ‚ (g v))) , (Î» u â†’ G(hâ‚‚ (f u)))
  
_âŠ—â‚_ : {A B C D : Obj} â†’ Hom A C â†’ Hom B D â†’ Hom (A âŠ—â‚’ B) (C âŠ—â‚’ D)
_âŠ—â‚_ {(U , X , Î±)}{(V , Y , Î²)}{(W , Z , Î³)}{(S , T , Î´)} (f , F , pâ‚) (g , G , pâ‚‚) = âŸ¨ f , g âŸ© , FâŠ— {f = f}{F}{g}{G} , p
 where
  p : {u : U Ã— V} {y : (S â†’ Z) Ã— (W â†’ T)} â†’ (Î± âŠ—áµ£ Î²) u ((FâŠ— {f = f}{F}{g}{G}) y) â†’ (Î³ âŠ—áµ£ Î´) (âŸ¨ f , g âŸ© u) y
  p {(u , v)} {(hâ‚ , hâ‚‚)} (p-Î± , p-Î²) = pâ‚ p-Î± , pâ‚‚ p-Î²

-- The unit for tensor:
Î¹ : âŠ¤ â†’ âŠ¤ â†’ Set
Î¹ triv triv = âŠ¤

I : Obj
I = (âŠ¤ , âŠ¤ , Î¹)

-- The left-unitor:
Î»âŠ—-p : âˆ€{U X Î±}{u : âŠ¤ Ã— U} {x : X} â†’ (Î¹ âŠ—áµ£ Î±) u ((Î» _ â†’ triv) , (Î» _ â†’ x)) â†’ Î± (snd u) x
Î»âŠ—-p {U}{X}{Î±}{(triv , u)}{x} (triv , p-Î±) = p-Î±
   
Î»âŠ— : âˆ€{A : Obj} â†’ Hom (I âŠ—â‚’ A) A
Î»âŠ— {(U , X , Î±)} = snd , (Î» x â†’ (Î» _ â†’ triv) , (Î» _ â†’ x)) , Î»âŠ—-p

Î»â»Â¹âŠ— : âˆ€{A : Obj} â†’ Hom A (I âŠ—â‚’ A)
Î»â»Â¹âŠ— {(U , X , Î±)} = (Î» u â†’ triv , u) , ((Î» x â†’ snd x triv) , Î»â»Â¹âŠ—-p)  
 where
  Î»â»Â¹âŠ—-p : âˆ€{U X Î±} â†’ {u : U} {y : (U â†’ âŠ¤) Ã— (âŠ¤ â†’ X)} â†’ Î± u (snd y triv) â†’ (Î¹ âŠ—áµ£ Î±) (triv , u) y
  Î»â»Â¹âŠ—-p {U}{X}{Î±}{u}{(hâ‚ , hâ‚‚)} p-Î± with hâ‚ u
  ... | triv = triv , p-Î±

-- The right-unitor:
ÏâŠ— : âˆ€{A : Obj} â†’ Hom (A âŠ—â‚’ I) A
ÏâŠ— {(U , X , Î±)} = fst , (Î» x â†’ (Î» xâ‚ â†’ x) , (Î» xâ‚ â†’ triv)) , ÏâŠ—-p
 where
  ÏâŠ—-p : âˆ€{U X Î±}{u : U Ã— âŠ¤}{x : X} â†’ (Î± âŠ—áµ£ Î¹) u ((Î» _ â†’ x) , (Î» _ â†’ triv)) â†’ Î± (fst u) x
  ÏâŠ—-p {U}{X}{Î±}{(u , triv)}{x} (p-Î± , triv) = p-Î±

ÏâŠ—-inv : âˆ€{A : Obj} â†’ Hom A (A âŠ—â‚’ I)
ÏâŠ—-inv {(U , X , Î±)} = (Î» x â†’ x , triv) , (Î» x â†’ fst x triv) , ÏâŠ—-p-inv
 where
  ÏâŠ—-p-inv : âˆ€{U X Î±}{u : U} {y : Î£ (âŠ¤ â†’ X) (Î» x â†’ U â†’ âŠ¤)} â†’ Î± u (fst y triv) â†’ (Î± âŠ—áµ£ Î¹) (u , triv) y
  ÏâŠ—-p-inv {U}{X}{Î±}{u}{(f , g)} p-Î± rewrite single-range {g = g}{u} = p-Î± , triv

-- Symmetry:
Î²âŠ— : âˆ€{A B : Obj} â†’ Hom (A âŠ—â‚’ B) (B âŠ—â‚’ A)
Î²âŠ— {(U , X , Î±)}{(V , Y , Î²)} = twist-Ã— , twist-Ã— , Î²âŠ—-p
 where
   Î²âŠ—-p : âˆ€{U V Y X Î± Î²} â†’ {u : U Ã— V} {y : (U â†’ Y) Ã— (V â†’ X)} â†’
       (Î± âŠ—áµ£ Î²) u (twist-Ã— y) â†’ (Î² âŠ—áµ£ Î±) (twist-Ã— u) y
   Î²âŠ—-p {U}{V}{Y}{X}{Î±}{Î²}{(u , v)}{(hâ‚ , hâ‚‚)} p-Î± = twist-Ã— p-Î±

-- The associator:
Î±âŠ—-inv : âˆ€{A B C : Obj} â†’ Hom (A âŠ—â‚’ (B âŠ—â‚’ C)) ((A âŠ—â‚’ B) âŠ—â‚’ C)
Î±âŠ—-inv {(U , X , Î±)}{(V , Y , Î²)}{(W , Z , Î³)} = rl-assoc-Ã— , FÎ±-inv , Î±-inv-cond
 where
   FÎ±-inv : (W â†’ (V â†’ X) Ã— (U â†’ Y)) Ã— (U Ã— V â†’ Z) â†’ (V Ã— W â†’ X) Ã— (U â†’ (W â†’ Y) Ã— (V â†’ Z))
   FÎ±-inv = (Î» p â†’ (Î» p' â†’ fst ((fst p) (snd p')) (fst p')) , (Î» u â†’ (Î» w â†’ snd (fst p w) u) , (Î» v â†’ (snd p) (u , v))))
   Î±-inv-cond : âˆ€{u : U Ã— V Ã— W} {y : (W â†’ (V â†’ X) Ã— (U â†’ Y)) Ã— (U Ã— V â†’ Z)} â†’ (Î± âŠ—áµ£ (Î² âŠ—áµ£ Î³)) u (FÎ±-inv y) â†’ ((Î± âŠ—áµ£ Î²) âŠ—áµ£ Î³) (rl-assoc-Ã— u) y
   Î±-inv-cond {(u , v , w)} {(hâ‚ , hâ‚‚)} (pâ‚ , pâ‚‚ , pâ‚ƒ) with hâ‚ w
   ... | (a , b) = (pâ‚ , pâ‚‚) , pâ‚ƒ

FÎ± : âˆ€{V W X Y U V Z : Set} â†’ Î£ (Î£ V (Î» x â†’ W) â†’ X) (Î» x â†’ U â†’ Î£ (W â†’ Y) (Î» xâ‚ â†’ V â†’ Z))
       â†’ Î£ (W â†’ Î£ (V â†’ X) (Î» x â†’ U â†’ Y)) (Î» x â†’ Î£ U (Î» xâ‚ â†’ V) â†’ Z)
FÎ± (f ,  g) = (Î» x â†’ (Î» xâ‚ â†’ f ((xâ‚ , x))) , (Î» xâ‚ â†’ fst (g xâ‚) x)) , (Î» x â†’ snd (g (fst x)) (snd x))

Î±âŠ— : âˆ€{A B C : Obj} â†’ Hom ((A âŠ—â‚’ B) âŠ—â‚’ C) (A âŠ—â‚’ (B âŠ—â‚’ C)) 
Î±âŠ— {(U , X , Î±)}{(V , Y , Î²)}{(W , Z , Î³)} = (lr-assoc-Ã— , FÎ± {V} , Î±-cond)
 where
  Î±-cond : {u : Î£ (Î£ U (Î» x â†’ V)) (Î» x â†’ W)}{y : Î£ (Î£ V (Î» x â†’ W) â†’ X) (Î» x â†’ U â†’ Î£ (W â†’ Y) (Î» xâ‚ â†’ V â†’ Z))} â†’
      ((Î± âŠ—áµ£ Î²) âŠ—áµ£ Î³) u (FÎ± {V} y) â†’ (Î± âŠ—áµ£ (Î² âŠ—áµ£ Î³)) (lr-assoc-Ã— u) y
  Î±-cond {(u , v) , w}{(f , g)} ((pâ‚ , pâ‚‚) , pâ‚ƒ) with g u
  ... | (hâ‚ , hâ‚‚) = pâ‚ , pâ‚‚ , pâ‚ƒ

-- Internal hom:
âŠ¸-cond : âˆ€{U V X Y : Set} â†’ (U â†’ X â†’ Set) â†’ (V â†’ Y â†’ Set) â†’ (U â†’ V) Ã— (Y â†’ X) â†’ U Ã— Y â†’ Set
âŠ¸-cond Î± Î² (f , g) (u , y) = Î± u (g y) â†’ Î² (f u) y

_âŠ¸â‚’_ : Obj â†’ Obj â†’ Obj
(U , X , Î±) âŠ¸â‚’ (V , Y , Î²) = ((U â†’ V) Ã— (Y â†’ X)) , (U Ã— Y) , âŠ¸-cond Î± Î²

_âŠ¸â‚_ : {A B C D : Obj} â†’ Hom C A â†’ Hom B D â†’ Hom (A âŠ¸â‚’ B) (C âŠ¸â‚’ D)
_âŠ¸â‚_ {(U , X , Î±)}{(V , Y , Î²)}{(W , Z , Î³)}{(S , T , Î´)} (f , F , pâ‚) (g , G , pâ‚‚) =
  h , H , pâ‚ƒ
  where
   h : Î£ (U â†’ V) (Î» x â†’ Y â†’ X) â†’ Î£ (W â†’ S) (Î» x â†’ T â†’ Z)
   h (hâ‚ , hâ‚‚) = (Î» w â†’ g (hâ‚ (f w))) , (Î» t â†’ F (hâ‚‚ (G t)))
   H : Î£ W (Î» x â†’ T) â†’ Î£ U (Î» x â†’ Y)
   H (w , t) = f w , G t
   pâ‚ƒ : {u : Î£ (U â†’ V) (Î» x â†’ Y â†’ X)} {y : Î£ W (Î» x â†’ T)} â†’ âŠ¸-cond Î± Î² u (H y) â†’ âŠ¸-cond Î³ Î´ (h u) y
   pâ‚ƒ {hâ‚ , hâ‚‚}{w , t} c c' = pâ‚‚ (c (pâ‚ c'))

-- The of-course exponential:
!â‚’-cond : âˆ€{U X : Set}
  â†’ (U â†’ X â†’ Set)
  â†’ U
  â†’ (U â†’ X *)
  â†’ Set
!â‚’-cond Î± u f = all-pred (Î± u) (f u)
   
!â‚’ : Obj â†’ Obj
!â‚’ (U , X , Î±) = U , (U â†’ X *) , !â‚’-cond Î±

!-cta : {V Y U X : Set}
  â†’ (Y â†’ X)
  â†’ (U â†’ V)
  â†’ (V â†’ Y *)
  â†’ (U â†’ X *)
!-cta F f g = Î» u â†’ list-funct F (g (f u))

!â‚-cond : âˆ€{U V Y X : Set}{F : Y â†’ X}{f : U â†’ V}
  â†’ (Î± : U â†’ X â†’ Set)
  â†’ (Î² : V â†’ Y â†’ Set)
  â†’ (p : {u : U} {y : Y} â†’ Î± u (F y) â†’ Î² (f u) y)
    {u : U}{l : Y *}
  â†’ all-pred (Î± u) (list-funct F l)
  â†’ all-pred (Î² (f u)) l
!â‚-cond _ _ _ {l = []} _ = triv
!â‚-cond Î± Î² p {u}{x :: xs} (p' , p'') = p p' , !â‚-cond Î± Î² p p''     
      
!â‚ : {A B : Obj} â†’ Hom A B â†’ Hom (!â‚’ A) (!â‚’ B)
!â‚ {U , X , Î±}{V , Y , Î²} (f , F , p) = f , !-cta F f , !â‚-cond Î± Î² p

-- Of-course is a comonad:
Îµ : âˆ€{A} â†’ Hom (!â‚’ A) A
Îµ {U , X , Î±} = id-set , (Î» x y â†’ [ x ]) , fst

Î´-cta : {U X : Set} â†’ (U â†’ ğ•ƒ (U â†’ ğ•ƒ X)) â†’ U â†’ ğ•ƒ X
Î´-cta g u = foldr (Î» f rest â†’ (f u) ++ rest) [] (g u)
  
Î´ : âˆ€{A} â†’ Hom (!â‚’ A) (!â‚’ (!â‚’ A))
Î´ {U , X , Î±} = id-set , Î´-cta , Î´-cond
  where
   Î´-cond : {u : U} {l : ğ•ƒ (U â†’ ğ•ƒ X)}
     â†’ all-pred (Î± u) (foldr (Î» f â†’ _++_ (f u)) [] l)
     â†’ all-pred (Î» f
     â†’ all-pred (Î± u) (f u)) l
   Î´-cond {l = []} _ = triv
   Î´-cond {u}{l = x :: l'} p with
     all-pred-append {X}{Î± u}
                     {x u}
                     {foldr (Î» f â†’ _++_ (f u)) [] l'}
                     âˆ§-unit âˆ§-assoc
   ... | p' rewrite p' = fst p , Î´-cond {u} {l'} (snd p)

-- These diagrams can be found on page 22 of the report.
comonand-diagâ‚ : âˆ€{A}
  â†’ (Î´ {A}) â—‹ (!â‚ (Î´ {A})) â‰¡h (Î´ {A}) â—‹ (Î´ { !â‚’ A})
comonand-diagâ‚ {U , X , Î±} =
  refl , ext-set (Î» {a} â†’ ext-set (Î» {aâ‚} â†’ aux {aâ‚}{a aâ‚}))
 where
   aux : âˆ€{aâ‚ : U}{l : ğ•ƒ (U â†’ ğ•ƒ (U â†’ ğ•ƒ X))} â†’
      foldr (Î» f â†’ _++_ (f aâ‚)) []
      (map (Î» g u â†’ foldr (Î» f â†’ _++_ (f u)) [] (g u)) l)
      â‰¡
      foldr (Î» f â†’ _++_ (f aâ‚)) [] (foldr (Î» f â†’ _++_ (f aâ‚)) [] l)
   aux {a}{[]} = refl  
   aux {a}{x :: l} rewrite
     sym (foldr-append {lâ‚ = x a}{foldr (Î» f â†’ _++_ (f a)) [] l}{a})
     = cong2 {a = foldr (Î» f â†’ _++_ (f a)) [] (x a)}
             _++_
             refl
             (aux {a}{l})

comonand-diagâ‚‚ : âˆ€{A}
  â†’ (Î´ {A}) â—‹ (Îµ { !â‚’ A}) â‰¡h (Î´ {A}) â—‹ (!â‚ (Îµ {A}))
comonand-diagâ‚‚ {U , X , Î±} =
  refl , ext-set (Î» {f} â†’ ext-set (Î» {a} â†’ aux {a}{f a}))
 where
  aux : âˆ€{a : U}{l : X *}
    â†’ l ++ [] â‰¡ foldr (Î» fâ‚ â†’ _++_ (fâ‚ a)) [] (map (Î» x y â†’ x :: []) l)
  aux {a}{[]} = refl
  aux {a}{x :: l} with aux {a}{l}
  ... | IH rewrite ++[] l =
    cong2 {a = x} {x} {l}
          {foldr (Î» fâ‚ â†’ _++_ (fâ‚ a)) [] (map (Î» xâ‚ y â†’ xâ‚ :: []) l)} _::_ refl
          IH
          
module Cartesian where
  Ï€â‚ : {U X V Y : Set}
    â†’ {Î± : U â†’ X â†’ Set}
    â†’ {Î² : V â†’ Y â†’ Set}
    â†’ Hom ((!â‚’ (U , X , Î±)) âŠ—â‚’ (!â‚’ (V , Y , Î²))) (!â‚’ (U , X , Î±))
  Ï€â‚ {U}{X}{V}{Y}{Î±}{Î²} = fst , (Î» f â†’ (Î» v u â†’ f u) , (Î» u v â†’ [])) , Ï€â‚-cond
    where
      Ï€â‚-cond : âˆ€{u : Î£ U (Î» x â†’ V)} {y : U â†’ ğ•ƒ X} â†’
        ((Î» uâ‚ f â†’ all-pred (Î± uâ‚) (f uâ‚)) âŠ—áµ£
        (Î» uâ‚ f â†’ all-pred (Î² uâ‚) (f uâ‚)))
        u ((Î» v uâ‚ â†’ y uâ‚) , (Î» uâ‚ v â†’ [])) â†’
        all-pred (Î± (fst u)) (y (fst u))
      Ï€â‚-cond {u , v}{f} (pâ‚ , pâ‚‚) = pâ‚

  Ï€â‚‚ : {U X V Y : Set}
      â†’ {Î± : U â†’ X â†’ Set}
      â†’ {Î² : V â†’ Y â†’ Set}
      â†’ Hom ((!â‚’ (U , X , Î±)) âŠ—â‚’ (!â‚’ (V , Y , Î²))) (!â‚’ (V , Y , Î²))
  Ï€â‚‚ {U}{X}{V}{Y}{Î±}{Î²} = snd , (Î» f â†’ (Î» v u â†’ []) , (Î» u v â†’ f v)) , Ï€â‚‚-cond
      where
        Ï€â‚‚-cond : âˆ€{u : Î£ U (Î» x â†’ V)} {y : V â†’ ğ•ƒ Y} â†’
          ((Î» uâ‚ f â†’ all-pred (Î± uâ‚) (f uâ‚)) âŠ—áµ£
            (Î» uâ‚ f â†’ all-pred (Î² uâ‚) (f uâ‚)))
              u ((Î» v uâ‚ â†’ []) , (Î» uâ‚ v â†’ y v)) â†’
            all-pred (Î² (snd u)) (y (snd u))
        Ï€â‚‚-cond {u , v}{f} (pâ‚ , pâ‚‚) = pâ‚‚

  cart-ar-crt : {U X V Y W Z : Set}
    â†’ {Î± : U â†’ X â†’ Set}
    â†’ {Î² : V â†’ Y â†’ Set}
    â†’ {Î³ : W â†’ Z â†’ Set}
    â†’ Hom (!â‚’ (W , Z , Î³)) (!â‚’ (U , X , Î±))
    â†’ Hom (!â‚’ (W , Z , Î³)) (!â‚’ (V , Y , Î²))
    â†’ Î£ (V â†’ U â†’ ğ•ƒ X) (Î» x â†’ U â†’ V â†’ ğ•ƒ Y) â†’ W â†’ ğ•ƒ Z
  cart-ar-crt  (f , F , pâ‚) (g , G , pâ‚‚) (jâ‚ , jâ‚‚) w = F (jâ‚ (g w)) w ++ G (jâ‚‚ (f w)) w

  cart-ar : {U X V Y W Z : Set}
    â†’ {Î± : U â†’ X â†’ Set}
    â†’ {Î² : V â†’ Y â†’ Set}
    â†’ {Î³ : W â†’ Z â†’ Set}
    â†’ Hom (!â‚’ (W , Z , Î³)) (!â‚’ (U , X , Î±))
    â†’ Hom (!â‚’ (W , Z , Î³)) (!â‚’ (V , Y , Î²))
    â†’ Hom (!â‚’ (W , Z , Î³)) ((!â‚’ (U , X , Î±)) âŠ—â‚’ (!â‚’ (V , Y , Î²)))
  cart-ar {U}{X}{V}{Y}{W}{Z}{Î±}{Î²}{Î³} (f , F , pâ‚) (g , G , pâ‚‚)
    = (Î» w â†’ f w , g w) , cart-ar-crt {Î± = Î±}{Î²} (f , F , pâ‚) (g , G , pâ‚‚) , cart-ar-cond
      where
        cart-ar-cond : âˆ€{u : W} {y : Î£ (V â†’ U â†’ ğ•ƒ X) (Î» x â†’ U â†’ V â†’ ğ•ƒ Y)} â†’
          all-pred (Î³ u) (cart-ar-crt {Î± = Î±}{Î²} (f , F , pâ‚) (g , G , pâ‚‚) y u) â†’
          ((Î» uâ‚ fâ‚ â†’ all-pred (Î± uâ‚) (fâ‚ uâ‚)) âŠ—áµ£
          (Î» uâ‚ fâ‚ â†’ all-pred (Î² uâ‚) (fâ‚ uâ‚)))
          (f u , g u) y
        cart-ar-cond {w}{jâ‚ , jâ‚‚} p
          rewrite
            all-pred-append {f = Î³ w}{F (jâ‚ (g w)) w}{G (jâ‚‚ (f w)) w} âˆ§-unit âˆ§-assoc with p
        ... | (a , b) = pâ‚ a , pâ‚‚ b

  cart-diagâ‚ : {U X V Y W Z : Set}
    â†’ {Î± : U â†’ X â†’ Set}
    â†’ {Î² : V â†’ Y â†’ Set}
    â†’ {Î³ : W â†’ Z â†’ Set}
    â†’ {f : Hom (W , Z , Î³) (U , X , Î±)}
    â†’ {g : Hom (W , Z , Î³) (V , Y , Î²)}
    â†’ _â‰¡h_ { !â‚’ (W , Z , Î³)}{ !â‚’ (U , X , Î±)}
      (!â‚ {W , Z , Î³}{U , X , Î±} f)
      (comp { !â‚’ (W , Z , Î³)}
            {((!â‚’ (U , X , Î±)) âŠ—â‚’ (!â‚’ (V , Y , Î²)))}
            { !â‚’ (U , X , Î±)}
            (cart-ar {Î± = Î±}{Î²}{Î³} (!â‚ {W , Z , Î³}{U , X , Î±} f) (!â‚ {W , Z , Î³}{V , Y , Î²} g))
            Ï€â‚)
  cart-diagâ‚ {f = f , F , pâ‚}{g , G , pâ‚‚}
    = refl , ext-set (Î» {j} â†’ ext-set (Î» {w} â†’ sym (++[] (map F (j (f w))))))

  cart-diagâ‚‚ : {U X V Y W Z : Set}
    â†’ {Î± : U â†’ X â†’ Set}
    â†’ {Î² : V â†’ Y â†’ Set}
    â†’ {Î³ : W â†’ Z â†’ Set}
    â†’ {f : Hom (W , Z , Î³) (U , X , Î±)}
    â†’ {g : Hom (W , Z , Î³) (V , Y , Î²)}
    â†’ _â‰¡h_ { !â‚’ (W , Z , Î³)}{ !â‚’ (V , Y , Î²)}
      (!â‚ {W , Z , Î³}{V , Y , Î²} g)
      (comp { !â‚’ (W , Z , Î³)}
            {((!â‚’ (U , X , Î±)) âŠ—â‚’ (!â‚’ (V , Y , Î²)))}
            { !â‚’ (V , Y , Î²)}
            (cart-ar {Î± = Î±}{Î²}{Î³} (!â‚ {W , Z , Î³}{U , X , Î±} f) (!â‚ {W , Z , Î³}{V , Y , Î²} g))
            Ï€â‚‚)
  cart-diagâ‚‚ {f = f , F , pâ‚}{g , G , pâ‚‚}
    = refl , ext-set (Î» {j} â†’ ext-set (Î» {w} â†’ refl))
