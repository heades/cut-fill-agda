-----------------------------------------------------------------------
-- This file formalizes the proof that Dialâ‚‚(Sets) is indeed a model --
-- of Full Tensorial Logic.  See Lemma ?? on page ?? of the paper.   --
-----------------------------------------------------------------------
module Tensorial where

open import prelude
open import Dial2Sets

-----------------------------------------------------------------------
-- We first must prove that Dialâ‚‚(Sets) is a dialogue category.      --
-- See Definition ?? on page ?? of the paper.                        --
-----------------------------------------------------------------------

-- This defines the negation functor: Â¬ : Dialâ‚‚(Sets) â†’ Dialâ‚‚áµ’áµ–(Sets)
Â¬â‚’ : Obj â†’ Obj
Â¬â‚’ (U , X , Î±) = (X , U , (Î» x u â†’ (Î± u x) â†’ âŠ¥))

Â¬â‚ : {A B : Obj} â†’ Hom A B â†’ Hom (Â¬â‚’ B) (Â¬â‚’ A)
Â¬â‚ {(U , X , Î±)}{(V , Y , Î²)} (f , F , p) = (F , f , (Î» x xâ‚ â†’ x (p xâ‚)))

-- Next we must define a family of bijections.
Ï† : {A B C : Obj} â†’ Hom (A âŠ—â‚’ B) (Â¬â‚’ C) â†’ Hom A (Â¬â‚’ (B âŠ—â‚’ C))
Ï† {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)} (f , F , pâ‚) =
  (Î» u â†’ (Î» w â†’ (snd (F w)) u) ,
  (Î» v â†’ f (u , v))) ,
  (Î» c â†’ fst (F (snd c)) (fst c)) ,
  c
 where
   G : V Ã— W â†’ X
   G = Î» c â†’ fst (F (snd c)) (fst c)
   c : âˆ€{u : U} {y : V Ã— W}
     â†’ Î± u (G y)
     â†’ (Î² âŠ—áµ£ Î³) y ((Î» w â†’ snd (F w) u) , (Î» v â†’ f (u , v)))
     â†’ âŠ¥
   c {u}{(v , w)} p' (p'' , p''') with F w | pâ‚ {u , v}{w}
   ... | (h1 , h2) | pâ‚‚ = pâ‚‚ (p' , p'') p'''

Ï†-inv : {A B C : Obj} â†’ Hom A (Â¬â‚’ (B âŠ—â‚’ C)) â†’ Hom (A âŠ—â‚’ B) (Â¬â‚’ C)
Ï†-inv {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)} (h , H , pâ‚) =
  (Î» c â†’ (snd (h (fst c))) (snd c)) ,
         (Î» w â†’ (Î» v â†’ H (v , w)) ,
         (Î» u â†’ (fst (h u)) w)) ,
         pâ‚‚
 where
  j : U Ã— V â†’ Z
  j = Î» c â†’ (snd (h (fst c))) (snd c)
  J : W â†’ (V â†’ X) Ã— (U â†’ Y)
  J = Î» w â†’ (Î» v â†’ H (v , w)) , (Î» u â†’ (fst (h u)) w)
  pâ‚‚ : âˆ€{i : U Ã— V} {w : W} â†’ (Î± âŠ—áµ£ Î²) i (J w) â†’ Î³ w (j i) â†’ âŠ¥
  pâ‚‚ {(u , v)} {w} (pâ‚ƒ , pâ‚„) pâ‚… with h u | pâ‚ {u}{(v , w)}
  ... | (hâ‚ , hâ‚‚) | pâ‚† = pâ‚† pâ‚ƒ (pâ‚„ , pâ‚…)

-- The following proves that Ï† and Ï†-inv are mutual inverse, and thus
-- define a bijection.
Ï†-bij-1 : âˆ€{A B C}{m : Hom (A âŠ—â‚’ B) (Â¬â‚’ C)}
  â†’ Ï†-inv (Ï† m) â‰¡h id-set m
Ï†-bij-1 {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)}{(h , H , pâ‚)}
  = eta-Ã— ext-set , ext-set aux
 where
   aux : {a : W} â†’ ((Î» v â†’ fst (H a) v) , (Î» u â†’ snd (H a) u)) â‰¡ H a
   aux {w} with H w
   ... | (hâ‚ , hâ‚‚) = refl

Ï†-bij-2 : âˆ€{A B C}{m : Hom A (Â¬â‚’ (B âŠ—â‚’ C))} â†’ Ï† (Ï†-inv m) â‰¡h id-set m
Ï†-bij-2 {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)}{(h , H , pâ‚)}
  = ext-set aux , eta-Ã— ext-set
 where
   aux : {a : U} â†’ ((Î» w â†’ fst (h a) w) , (Î» v â†’ snd (h a) v)) â‰¡ h a
   aux {u} with h u
   ... | (hâ‚ , hâ‚‚)= refl

-- The following shows that Ï† {A}{B}{C} is natural in A, B, and C.
Ï†-nat-1 : âˆ€{A A' B C}{n : Hom A' A}{m : Hom (A âŠ—â‚’ B) (Â¬â‚’ C)}
        â†’    Homâ‚ n (id {Â¬â‚’ (B âŠ—â‚’ C)}) (Ï† {A}{B}{C} m)
          â‰¡h Ï† {A'} {B} {C} (Homâ‚ (n âŠ—â‚ (id {B})) (id {Â¬â‚’ C}) m)
Ï†-nat-1 {(U , X , Î±)} {(V , Y , Î²)} {(V' , Y' , Î²')} {(W , Z , Î³)} {(n , N , pn)} {(m , M , pm)} =
 ext-set (Î» {v} â†’ eq-Ã— (ext-set (Î» {w} â†’ aux {w})) (ext-set refl)) , ext-set (Î» {a} â†’ aux' {a})
 where
   aux : âˆ€{w v} â†’ snd (M w) (n v) â‰¡ snd (FâŠ— {f = n}{N}{id-set}{id-set} (M w)) v
   aux {w} with M w
   ... | (hâ‚ , hâ‚‚) = refl

   aux' : âˆ€{a} â†’ N (fst (M (snd a)) (fst a)) â‰¡ fst (FâŠ— {f = n}{N}{id-set}{id-set} (M (snd a))) (fst a)
   aux' {(v' , w)} with M w
   ... | (hâ‚ , hâ‚‚) = refl
   
Ï†-nat-2 : âˆ€{A B B' C}{n : Hom B' B}{m : Hom (A âŠ—â‚’ B) (Â¬â‚’ C)}
  â†’    Homâ‚ (id {A}) (Â¬â‚ (n âŠ—â‚ id {C})) (Ï† {A} {B} {C} m)
    â‰¡h Ï† {A} {B'} {C} (Homâ‚ ((id {A} âŠ—â‚ n)) (id {Â¬â‚’ C}) m)
Ï†-nat-2 {(U , X , Î±)} {(V , Y , Î²)} {(V' , Y' , Î²')} {(W , Z , Î³)} {(n , N , pn)} {(m , M , pm)} =
  let f = Î» x â†’ fst (M (snd (âŸ¨ n , (Î» xâ‚ â†’ xâ‚) âŸ© x))) (fst (âŸ¨ n , (Î» xâ‚ â†’ xâ‚) âŸ© x))
      g = Î» c â†’ fst (FâŠ— (M (snd c))) (fst c)
   in ext-set (Î» {u} â†’ eq-Ã— (ext-set aux) refl) , ext-set {f = f}{g} (Î» {a} â†’ aux' {a})
 where
  aux : {u : U}{a : W} â†’ N (snd (M a) u) â‰¡ snd (FâŠ— {f = id-set}{id-set}{n}{N} (M a)) u
  aux {u}{a} with M a
  ... | (hâ‚ , hâ‚‚) = refl

  aux' : {a : Î£ V' (Î» x â†’ W)}
       â†’   fst (M (snd (âŸ¨ n , (Î» x â†’ x) âŸ© a))) (fst (âŸ¨ n , (Î» x â†’ x) âŸ© a))
         â‰¡ fst (FâŠ— {f = id-set}{id-set}{n}{N} (M (snd a))) (fst a)
  aux' {v' , w} with M w
  ... | (hâ‚ , hâ‚‚) = refl

Ï†-nat-3 : âˆ€{A B C C'}{n : Hom C' C}{m : Hom (A âŠ—â‚’ B) (Â¬â‚’ C)}
        â†’ Homâ‚ (id {A}) (Â¬â‚ ((id {B}) âŠ—â‚ n)) (Ï† {A}{B}{C} m) â‰¡h Ï† {A} {B} {C'} (Homâ‚ (id {A âŠ—â‚’ B}) (Â¬â‚ n) m)
Ï†-nat-3 {(U , X , Î±)} {(V , Y , Î²)} {(V' , Y' , Î²')} {(W , Z , Î³)} {(n , N , pn)} {(m , M , pm)}
 = refl , ext-set (Î» {a} â†’ aux {a}) 
 where
   aux : âˆ€{a} â†’ fst (M (snd (âŸ¨ (Î» x â†’ x) , n âŸ© a))) (fst (âŸ¨ (Î» x â†’ x) , n âŸ© a)) â‰¡ fst (M (n (snd a))) (fst a)
   aux {(v , w)} = refl

-- Finally, Ï† must adhere to a coherence diagrams.  See Definition ??
-- on page ?? of the paper for the diagram.
Ï†-coh : âˆ€{A B C D}{m : Hom (A âŠ—â‚’ (B âŠ—â‚’ C)) (Â¬â‚’ D)}
  â†’    Ï† (Ï† (Homâ‚ (Î±âŠ— {A} {B} {C}) (id {Â¬â‚’ D}) m))
    â‰¡h Homâ‚ (id {A}) (Â¬â‚ (Î±âŠ—-inv {B} {C} {D})) (Ï† m)
Ï†-coh {(U , X , Î±)} {(V , Y , Î²)} {(W , Z , Î³)} {(S , T , Î´)} {(m , M , pm)}
  = ext-set (Î» {u} â†’ eq-Ã— aux (ext-set (Î» {v} â†’ eq-Ã— aux'' refl))) , ext-set (Î» {a} â†’ aux'''' {a})
 where
   aux : âˆ€{u} â†’ (Î» w â†’ snd (fst (FÎ± {V} (M (snd w))) (fst w)) u) â‰¡
      (Î» p' â†’ fst (snd (M (snd p')) u) (fst p'))
   aux {u} = ext-set (Î» {a} â†’ aux' {a})
    where
     aux' : {a : Î£ W (Î» x â†’ S)} â†’ snd (fst (FÎ± {V} (M (snd a))) (fst a)) u â‰¡ fst (snd (M (snd a)) u) (fst a)
     aux' {w , s} with M s
     ... | (hâ‚ , hâ‚‚) = refl

   aux'' : âˆ€{u v} â†’ (Î» w â†’ snd (FÎ± {V} (M w)) (u , v)) â‰¡ (Î» w â†’ snd (snd (M w) u) v)
   aux'' {u}{v} = ext-set aux'''
    where
      aux''' : {a : S} â†’ snd (FÎ± {V} (M a)) (u , v) â‰¡ snd (snd (M a) u) v
      aux''' {s} with M s
      ... | (hâ‚ , hâ‚‚) = refl

   aux'''' : âˆ€{a}
     â†’   fst (fst (FÎ± {V} (M (snd (snd a)))) (fst (snd a))) (fst a)
       â‰¡ fst (M (snd (rl-assoc-Ã— a))) (fst (rl-assoc-Ã— a))
   aux'''' {v , w , s} with M s
   ... | (hâ‚ , hâ‚‚) = refl

-----------------------------------------------------------------------
-- A dialouge category is a model of multiplicative tensor logic.    --
-- Now Dialâ‚‚(Sets) is a model of multiplicative additive tensor      --
-- logic, because we know Dialâ‚‚(Sets) has coproducts that distribute --
-- over tensor; Proposition 28 on page 52 of Valeria's thesis.       --
--                                                                   --
-----------------------------------------------------------------------

-- The following shows that Dialâ‚‚(Sets)! is cartesian.

Jâ‚’ = !â‚’

-- First, we define the cartesian product in Dialâ‚‚(Sets), and then use
-- Jâ‚’ to put us inside of Dialâ‚‚(Sets)!.
_&áµ£_ : {U X V Y : Set}
  â†’ (Î± : U â†’ X â†’ Set)
  â†’ (Î² : V â†’ Y â†’ Set)
  â†’ Î£ U (Î» x â†’ V)
  â†’ X âŠ Y
  â†’ Set
_&áµ£_ Î± Î² (u , v) (injâ‚ x) = Î± u x
_&áµ£_ Î± Î² (u , v) (injâ‚‚ y) = Î² v y

_&â‚’_ : (A B : Obj) â†’ Obj
(U , X , Î±) &â‚’ (V , Y , Î²) = (U Ã— V) , (X âŠ Y) , Î± &áµ£ Î²

-- The remainder of this file will work under the Jâ‚’ functor which
-- will put us inside of Dialâ‚‚(Sets)!.

-- This defines the projection morphism: Ï€â‚ : F(A & B) â†’ F(A).
Ï€â‚ : {A B : Obj} â†’ Hom (Jâ‚’ (A &â‚’ B)) (Jâ‚’ A)
Ï€â‚ {U , X , Î±}{V , Y , Î²} =
  fst ,
  (Î» (f : U â†’ (X *)) (p : U Ã— V) â†’ map injâ‚ (f (fst p))) ,
  Î» {u}{y} p â†’ Ï€â‚-cond {u}{y} p
 where
  Ï€â‚-cond : {u : Î£ U (Î» x â†’ V)} {y : U â†’ ğ•ƒ X} â†’
      all-pred ((Î± &áµ£ Î²) u) (map injâ‚ (y (fst u))) â†’
      all-pred (Î± (fst u)) (y (fst u))
  Ï€â‚-cond {u , v} y = aux y
   where
     aux : {l : X *}
       â†’ all-pred ((Î± &áµ£ Î²) (u , v)) (map injâ‚ l) â†’ all-pred (Î± u) l
     aux {[]} triv = triv
     aux {x :: l} (jâ‚ , jâ‚‚) = jâ‚ , aux jâ‚‚

-- This defines the projection morphism: Ï€â‚‚ : A & B â†’ B.
Ï€â‚‚ : {A B : Obj} â†’ Hom (Jâ‚’ (A &â‚’ B)) (Jâ‚’ B)
Ï€â‚‚ {U , X , Î±}{V , Y , Î²} =
  snd , (Î» f p â†’ map injâ‚‚ (f (snd p))) , Î» {u}{y} p â†’ Ï€â‚‚-cond {u}{y} p
 where
  Ï€â‚‚-cond : {u : Î£ U (Î» x â†’ V)} {y : V â†’ ğ•ƒ Y} â†’
      all-pred ((Î± &áµ£ Î²) u) (map injâ‚‚ (y (snd u))) â†’
      all-pred (Î² (snd u)) (y (snd u))
  Ï€â‚‚-cond {u , v} y = aux y
   where
     aux : {l : Y *}
       â†’ all-pred ((Î± &áµ£ Î²) (u , v)) (map injâ‚‚ l) â†’ all-pred (Î² v) l
     aux {[]} triv = triv
     aux {x :: l} (jâ‚ , jâ‚‚) = jâ‚ , aux jâ‚‚

cart-ar-crt : {U X V Y W Z : Set}
  â†’ {Î± : U â†’ X â†’ Set}
  â†’ {Î² : V â†’ Y â†’ Set}
  â†’ {Î³ : W â†’ Z â†’ Set}
  â†’ Hom (Jâ‚’ (W , Z , Î³)) (Jâ‚’ (U , X , Î±))
  â†’ Hom (Jâ‚’ (W , Z , Î³)) (Jâ‚’ (V , Y , Î²))
  â†’ (Î£ U (Î» x â†’ V) â†’ ğ•ƒ (X âŠ Y)) â†’ W â†’ ğ•ƒ Z
cart-ar-crt  (f , F , pâ‚) (g , G , pâ‚‚) j w
  with (Î» u â†’ (proj-âŠâ‚ (j (u , g w)))) | (Î» v â†’ (proj-âŠâ‚‚ (j (f w , v))))
... | jâ‚ | jâ‚‚ = F jâ‚ w ++ G jâ‚‚ w 

-- This takes two morphisms f : C â†’ A and g : C â†’ B, and constructs
-- a morphism (f,g) : C â†’ A & B.
cart-ar : {C A B : Obj}
  â†’ Hom (Jâ‚’ C) (Jâ‚’ A)
  â†’ Hom (Jâ‚’ C) (Jâ‚’ B)
  â†’ Hom (Jâ‚’ C) (Jâ‚’ (A &â‚’ B))
cart-ar {W , Z , Î³}{U , X , Î±}{V , Y , Î²} (f , F , pâ‚) (g , G , pâ‚‚)
  = (Î» w â†’ (f w , g w)) ,
    cart-ar-crt {Î± = Î±}{Î²}{Î³} (f , F , pâ‚) (g , G , pâ‚‚) ,
    (Î» {u}{y} p â†’ cart-ar-cond {u}{y} p)
  where
    cart-ar-cond : {u : W} {y : Î£ U (Î» x â†’ V) â†’ ğ•ƒ (X âŠ Y)} â†’
      all-pred (Î³ u)
      (F (Î» uâ‚ â†’ proj-âŠâ‚ (y (uâ‚ , g u))) u ++
       G (Î» v â†’ proj-âŠâ‚‚ (y (f u , v))) u) â†’
      all-pred ((Î± &áµ£ Î²) (f u , g u)) (y (f u , g u))
    cart-ar-cond {u}{j} p
      rewrite
        all-pred-append
          {f = Î³ u}
          {F (Î» uâ‚ â†’ (proj-âŠâ‚ (j (uâ‚ , g u)))) u}
          {G (Î» v â†’ (proj-âŠâ‚‚ (j (f u , v)))) u}
          âˆ§-unit âˆ§-assoc
     with p
    ... | (râ‚ , râ‚‚) = aux (pâ‚ râ‚) (pâ‚‚ râ‚‚)
     where
       aux : âˆ€{l}
         â†’ all-pred (Î± (f u)) ((proj-âŠâ‚ l))
         â†’ all-pred (Î² (g u)) ((proj-âŠâ‚‚ l))
         â†’ all-pred ((Î± &áµ£ Î²) (f u , g u)) l
       aux {[]} _ _ = triv
       aux {injâ‚ x :: l} (sâ‚ , sâ‚‚) xâ‚‚ = sâ‚ , aux {l} sâ‚‚ xâ‚‚
       aux {injâ‚‚ y :: l} xâ‚ (sâ‚ , sâ‚‚) = sâ‚ , aux {l} xâ‚ sâ‚‚

-- This shows that f â‰¡ (f,g);Ï€â‚.
cart-diagâ‚ : {A B C : Obj}
  â†’ {f : Hom C A}
  â†’ {g : Hom C B}
  â†’ _â‰¡h_ { Jâ‚’ C}{ Jâ‚’ A}
    (!â‚ {C}{A} f)
    (comp { Jâ‚’ C}
          {(Jâ‚’ (A &â‚’ B))}
          { Jâ‚’ A}
          (cart-ar
            (!â‚ {C}{A} f) (!â‚ {C}{B} g))
          Ï€â‚)
cart-diagâ‚ {U , X , Î±}{V , Y , Î²}{W , Z , Î³}{f = f , F , pâ‚}{g , G , pâ‚‚}
  = refl , ext-set (Î» {jâ‚} â†’ ext-set (Î» {w} â†’ aux))
  where
    aux : âˆ€{l : X *} â†’
      map F l â‰¡
      map F (proj-âŠâ‚ {_}{_}{X}{Y} (map injâ‚ l)) ++
      map G (proj-âŠâ‚‚ (map injâ‚ l))
    aux {l} rewrite
      map-proj-âŠâ‚ {_}{_}{X}{Y} l |
      map-proj-âŠâ‚‚-[] {_}{_}{X}{Y} l = sym (++[] (map F l))

-- This shows that g â‰¡ (f,g);Ï€â‚‚.
cart-diagâ‚‚ : {A B C : Obj}
  â†’ {f : Hom C A}
  â†’ {g : Hom C B}
  â†’ _â‰¡h_ { Jâ‚’ C}{ Jâ‚’ B}
    (!â‚ {C}{B} g)
    (comp { Jâ‚’ C}
          {(Jâ‚’ (A &â‚’ B))}
          { Jâ‚’ B}
          (cart-ar
            (!â‚ {C}{A} f) (!â‚ {C}{B} g))
          Ï€â‚‚)
cart-diagâ‚‚ {U , X , Î±}{V , Y , Î²}{W , Z , Î³}{f = f , F , pâ‚}{g , G , pâ‚‚}
  = refl , ext-set (Î» {jâ‚} â†’ ext-set (Î» {w} â†’ aux))
  where
    aux : âˆ€{l : Y *} â†’
      map G l â‰¡
      map F (proj-âŠâ‚ {_}{_}{X}{Y} (map injâ‚‚ l)) ++
      map G (proj-âŠâ‚‚ {_}{_}{X}{Y} (map injâ‚‚ l))
    aux {l} rewrite map-proj-âŠâ‚‚ {_}{_}{X}{Y} l |
                    map-proj-âŠâ‚-[] {_}{_}{X}{Y} l = refl

term-diag : âˆ€{A : Obj} â†’ Hom (Jâ‚’ A) (Jâ‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))
term-diag {U , X , Î±} = (Î» x â†’ triv) , (Î» f u â†’ aux (f triv) u) , (Î» {u}{y} â†’ aux' {u}{y triv})
 where
   aux : ğ•ƒ âŠ¥ â†’ U â†’ ğ•ƒ X
   aux [] u = []
   aux (x :: l) u = âŠ¥-elim x :: aux l u

   aux' : {u : U} {l : ğ•ƒ âŠ¥} â†’ all-pred (Î± u) (aux l u) â†’ all-pred (Î» yâ‚ â†’ âŠ¤) l
   aux' {u}{l = []} p = p
   aux' {u}{l = x :: l} (p , p') = triv , aux' {u}{l} p' 

term-cart-crtâ‚ : {X : Set} â†’ ğ•ƒ (X âŠ âŠ¥) â†’ ğ•ƒ X
term-cart-crtâ‚ [] = []
term-cart-crtâ‚ (injâ‚ x :: l) = x :: term-cart-crtâ‚ l
term-cart-crtâ‚ (injâ‚‚ y :: l) = âŠ¥-elim y :: term-cart-crtâ‚ l
   
term-cartâ‚ : âˆ€{A : Obj} â†’ Hom (Jâ‚’ A) (Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤)))
term-cartâ‚ {U , X , Î±} = (Î» x â†’ x , triv) , (Î» f u â†’ term-cart-crtâ‚ (f (u , triv))) , cond
 where   
   cond : {u : U} {l : ğ•ƒ (X âŠ âŠ¥)} â†’
      all-pred (Î± u) (term-cart-crtâ‚ l) â†’
      all-pred ((Î± &áµ£ (Î» x yâ‚ â†’ âŠ¤)) (u , triv)) l
   cond {u}{[]} p = triv
   cond {u}{injâ‚ x :: l} (p , p') = p , cond p'
   cond {u}{injâ‚‚ y :: l} (p , p') = triv , cond p'

term-cartâ‚‚ : âˆ€{A : Obj} â†’ Hom (Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))) (Jâ‚’ A)
term-cartâ‚‚ {U , X , Î±} = Ï€â‚

term-cart-isoâ‚ : âˆ€{A : Obj} â†’ _â‰¡h_ {Jâ‚’ A} {Jâ‚’ A} (comp {Jâ‚’ A}{Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))}{Jâ‚’ A} term-cartâ‚ term-cartâ‚‚) id
term-cart-isoâ‚ {U , X , Î±} = refl , ext-set (Î» {f} â†’ ext-set (Î» {u} â†’ aux))
 where
   aux : âˆ€{l : X *} â†’ term-cart-crtâ‚ (map injâ‚ l) â‰¡ l
   aux {[]} = refl
   aux {x :: l} rewrite aux {l} = refl

term-cart-isoâ‚‚ : âˆ€{A : Obj} â†’ _â‰¡h_ {Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))} {Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))}
  (comp {Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))}{Jâ‚’ A}{Jâ‚’ (A &â‚’ (âŠ¤ , âŠ¥ , Î» x y â†’ âŠ¤))} term-cartâ‚‚ term-cartâ‚) id
term-cart-isoâ‚‚ {U , X , Î±} = ext-set aux , ext-set (Î» {f} â†’ ext-set (aux' {f}))
 where
   aux : {a : Î£ U (Î» x â†’ âŠ¤)} â†’ (fst a , triv) â‰¡ a
   aux {u , triv} = refl

   aux' : {f : Î£ U (Î» x â†’ âŠ¤) â†’ ğ•ƒ (X âŠ âŠ¥)}{a : Î£ U (Î» x â†’ âŠ¤)} â†’ map injâ‚ (term-cart-crtâ‚ (f (fst a , triv))) â‰¡ f a
   aux' {f}{u , triv} = aux''
    where
      aux'' : âˆ€{l : (X âŠ âŠ¥) *} â†’ map injâ‚ (term-cart-crtâ‚ l) â‰¡ l
      aux'' {[]} = refl
      aux'' {injâ‚ x :: l} rewrite aux'' {l} = refl
      aux'' {injâ‚‚ y :: l} = âŠ¥-elim y

twist-cart : âˆ€{A B : Obj} â†’ Hom (Jâ‚’ (A &â‚’ B)) (Jâ‚’ (B &â‚’ A)) 
twist-cart {A}{B} = cart-ar {A &â‚’ B} {B} {A} Ï€â‚‚ Ï€â‚

